#+TITLE: Emacs configuration
#+PROPERTY: header-args:emacs-lisp :tangle init-tangled.el :results silent
#+STARTUP: overview

#+begin_src emacs-lisp :tangle no
(native-comp-available-p)
#+end_src

My favorite way to install emacs on OSX
#+begin_src sh :tangle no
brew tap d12frosted/emacs-plus
brew install --cask emacs-plus-app
#+end_src

#+begin_src emacs-lisp :tangle no
(native-compile-async "~/.emacs.d/elpa/" 'recursively)
#+end_src

* Configuration
** Initialization
#+BEGIN_SRC emacs-lisp
;;; -*- lexical-binding: t; -*-


(defun my-reload-dir-locals-for-current-buffer ()
  "Reloads dir locals for the current buffer."
  (interactive)
  (let ((enable-local-variables :all))
    (hack-dir-local-variables-non-file-buffer)))

(defun my-reload-dir-locals-for-all-buffer-in-this-directory ()
  "For every buffer with the same `default-directory` as the current buffer's, reload dir-locals."
  (interactive)
  (let ((dir default-directory))
    (dolist (buffer (buffer-list))
      (with-current-buffer buffer
        (when (equal default-directory dir)
          (my-reload-dir-locals-for-current-buffer))))))

(setq initial-major-mode 'fundamental-mode
      initial-scratch-message nil
      inhibit-startup-message t)

(pixel-scroll-precision-mode)


(put 'narrow-to-region 'disabled nil)


(defun abo/tangle-init-org ()
  "Tangle init.org into init-tangled.el manually."
  (interactive)
  (require 'org)
  (let ((org-file (expand-file-name "init.org" user-emacs-directory))
        (out-file (expand-file-name "init-tangled.el" user-emacs-directory)))
    (org-babel-tangle-file org-file out-file)
    (message "Tangled %s" out-file)))
#+END_SRC

** abo-initialization
#+BEGIN_SRC emacs-lisp
(defvar abo/default-file-name-handler-alist file-name-handler-alist)
(defvar abo/default-gc-cons-threshold (* 16 1024 1024))
(defvar abo/default-gc-cons-percentage 0.1)

(setq gc-cons-threshold most-positive-fixnum
      gc-cons-percentage 0.6
      file-name-handler-alist nil)

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold abo/default-gc-cons-threshold
                  gc-cons-percentage abo/default-gc-cons-percentage
                  file-name-handler-alist abo/default-file-name-handler-alist)
            (garbage-collect)))

;; Run GC while idle to reduce pauses during interactive editing.
(run-with-idle-timer
 5 t
 (lambda ()
   (unless (minibufferp)
     (garbage-collect))))

(setq read-process-output-max (* 1024 1024)
      process-adaptive-read-buffering nil)

(when (boundp 'inhibit-compacting-font-caches)
  (setq inhibit-compacting-font-caches t))
(when (boundp 'fast-but-imprecise-scrolling)
  (setq fast-but-imprecise-scrolling t))
(when (boundp 'redisplay-skip-fontification-on-input)
  (setq redisplay-skip-fontification-on-input t))
(when (boundp 'native-comp-async-report-warnings-errors)
  (setq native-comp-async-report-warnings-errors 'silent))

(setq byte-compile-warnings '(cl-functions))

(setq package-archives
      '(("melpa"       . "https://melpa.org/packages/")
        ("gnu"         . "https://elpa.gnu.org/packages/")
        ("nongnu"      . "https://elpa.nongnu.org/nongnu/")))

(setq package-user-dir (concat user-emacs-directory "elpa"))

(setq package--init-file-ensured t) ; do not add things at the end of init.el
(setq package-check-signature 'allow-unsigned)
(setq package-quickstart t)

(package-initialize)

(defun abo/refresh-package-contents ()
  "Refresh package archives on demand."
  (interactive)
  (package-refresh-contents))

(defun require-package (package &optional min-version)
  "Ask elpa to install given PACKAGE. You can specify a MIN-VERSION for your PACKAGE."
  (unless (package-installed-p package min-version)
    (unless package-archive-contents
      (package-refresh-contents))
    (package-install package)))

(require-package 'use-package)
(require 'use-package)

(require-package 'diminish)
(require 'diminish)

(setq use-package-compute-statistics t) ;(use-package-report)
(setq use-package-always-ensure t)

(setq custom-file (concat user-emacs-directory "custom.el"))
(condition-case err
    (load custom-file 'noerror 'nomessage)
  (error
   (message "Skipping invalid custom.el entry: %s" (error-message-string err))))

(if (file-exists-p "~/.emacs.d/.emacs-local")
    (load "~/.emacs.d/.emacs-local"))

(let ((site-lisp-dir (expand-file-name "site-lisp" user-emacs-directory)))
  (when (file-directory-p site-lisp-dir)
    (add-to-list 'load-path site-lisp-dir)
    (dolist (dir (directory-files site-lisp-dir t "^[^.]" t))
      (when (and (file-directory-p dir)
                 (directory-files dir nil "\\.el\\'" t))
        (add-to-list 'load-path dir)))))
#+END_SRC

** Environment and paths
#+BEGIN_SRC emacs-lisp
(let ((my-java-home "/Library/Java/JavaVirtualMachines/adoptopenjdk-12.0.2.jdk/Contents/Home"))
  (when (file-directory-p my-java-home)
    (setenv "JAVA_HOME" my-java-home)))
(setenv "OBJC_DISABLE_INITIALIZE_FORK_SAFETY" "YES")

(let* ((home-folder (getenv "HOME"))
       (my-paths `("/opt/homebrew/bin"
                   "/Users/abottazini/work/jeancaisse/node_modules/.bin"
                   "/Applications/Postgres.app/Contents/Versions/latest/bin"
                   "/opt/homebrew/opt/grep/libexec/gnubin"
                   "/opt/homebrew/opt/gnu-sed/libexec/gnubin"
                   "/opt/homebrew/opt/findutils/libexec/gnubin"
                   "/opt/homebrew/opt/coreutils/libexec/gnubin"
                   ,(concat home-folder "/.asdf/shims/")
                   ,(concat home-folder "/.config/yarn/global/node_modules/.bin/")
                   ,(concat home-folder "/.local/share/n/bin")
                   ,(concat home-folder "/work/dox-compose/bin/")
                   ,(concat home-folder "/dotfiles/bin/")
                   ,(concat home-folder "/.fzf/bin")
                   ,(concat home-folder "/.local/bin")
                   ,(concat home-folder "/.local/share/npm/bin/")
                   ,(concat home-folder "/bin")
                   "/snap/bin"
                   "/usr/local/bin"
                   "/bin/"
                   "/usr/bin/"
                   "/usr/local/sbin/"
                   "/opt/homebrew/opt/openjdk/bin/"
                   ,(concat home-folder "/.cargo/bin/"))))

  (setenv "PATH" (concat (mapconcat 'identity my-paths ":" ) ":"))
  (setq eshell-path-env (concat (mapconcat 'identity my-paths ":" ) ":"))
  (setq exec-path my-paths))

(use-package exec-path-from-shell
  :if (memq window-system '(mac ns x))
  :commands exec-path-from-shell-copy-env)

(when (memq window-system '(mac ns x))
  (exec-path-from-shell-copy-env "SSH_AGENT_PID")
  (exec-path-from-shell-copy-env "SSH_AUTH_SOCK"))

(setenv "FZF_DEFAULT_OPTS" "--color=light")
#+END_SRC

** Performance
#+BEGIN_SRC emacs-lisp
(setq large-file-warning-threshold (* 2 1024 1024))

(defun abo/check-large-file-hook ()
  "Use a lightweight setup for very large local text files."
  (let* ((file (buffer-file-name))
         (ext (downcase (or (file-name-extension (or file "")) ""))))
    (when (and file
               (not (file-remote-p file))
               (> (buffer-size) large-file-warning-threshold)
               (not (member ext '("jpg" "jpeg" "png" "gif" "webp" "svg" "pdf"))))
      (fundamental-mode)
      (font-lock-mode -1)
      (setq-local buffer-read-only t)
      (buffer-disable-undo))))
(add-hook 'find-file-hook #'abo/check-large-file-hook)

(setq-default bidi-paragraph-direction 'left-to-right)
(setq bidi-inhibit-bpa t)
(setq auto-window-vscroll nil)

(global-so-long-mode 1)
#+END_SRC

** Backups and autosave
#+BEGIN_SRC emacs-lisp
(let ((save-dir (expand-file-name "~/.local/share/emacs-saves/")))
  (unless (file-directory-p save-dir)
    (make-directory save-dir t))
  (setq backup-by-copying t
      backup-directory-alist `((".*" . ,save-dir))
      delete-old-versions t
      kept-new-versions 6
      kept-old-versions 2
      delete-by-moving-to-trash t
      auto-save-default t
      auto-save-timeout 60
      auto-save-interval 200
      version-control t
      create-lockfiles nil
      auto-save-file-name-transforms `((".*" ,save-dir t))))

(setq global-auto-revert-non-file-buffers t)
(setq auto-revert-verbose nil)
(setq auto-revert-remote-files nil)
(global-auto-revert-mode 1)
#+END_SRC

** Encoding
#+BEGIN_SRC emacs-lisp
(setq default-file-name-coding-system 'utf-8-unix)
(setq file-name-coding-system 'utf-8-unix)
(setq default-process-coding-system '(utf-8-unix . utf-8-unix))
(prefer-coding-system 'utf-8)
(modify-coding-system-alist 'process "\\*compilation\\*\\'" 'utf-8)
#+END_SRC

** General settings
#+BEGIN_SRC emacs-lisp
(setq help-window-select t
      default-indicate-empty-lines nil)

(make-variable-buffer-local 'compile-command)
(defalias 'yes-or-no-p 'y-or-n-p)
(setq ring-bell-function 'ignore)
(setq visible-bell t)

(pending-delete-mode 1)

(setq sentence-end-double-space nil)

(add-hook 'shell-mode-hook 'compilation-shell-minor-mode)

(setq-default indent-tabs-mode nil
              c-basic-offset 2)

(recentf-mode 1)
(setq recentf-max-menu-items 200)
(setq recentf-max-saved-items 200)
(setq recentf-auto-cleanup 'never)
(dolist (pattern '("/tmp/" "/ssh:" "/sudo:" "/scp:"))
  (add-to-list 'recentf-exclude pattern))

(let ((state-dir (expand-file-name ".cache/state/" user-emacs-directory)))
  (unless (file-directory-p state-dir)
    (make-directory state-dir t))
  (setq savehist-file (expand-file-name "history" state-dir)
        save-place-file (expand-file-name "places" state-dir)))
(savehist-mode 1)
(save-place-mode 1)
(setq history-length 200)
(setq savehist-additional-variables
      '(mark-ring global-mark-ring search-ring regexp-search-ring))

(setq warning-minimum-level :error)
#+END_SRC

** Whitespace
#+BEGIN_SRC emacs-lisp
(setq-default whitespace-style '(face trailing))

(defun abo/delete-trailing-whitespace-maybe ()
  "Delete trailing whitespace in regular text/code buffers."
  (when (and (derived-mode-p 'prog-mode 'text-mode 'conf-mode)
             (not buffer-read-only)
             (< (buffer-size) (* 1024 1024)))
    (delete-trailing-whitespace)))

(add-hook 'before-save-hook #'abo/delete-trailing-whitespace-maybe)
(add-hook 'prog-mode-hook 'whitespace-mode)
(eval-after-load "whitespace"
  '(diminish 'whitespace-mode))
#+END_SRC

** UI and display
#+BEGIN_SRC emacs-lisp
(global-display-line-numbers-mode -1)
(defun show-line-numbers ()
  (interactive)
  (setq display-line-numbers 'absolute))
(defun hide-line-numbers ()
  (interactive)
  (setq display-line-numbers 'nil))
(defun show-relative-line-numbers ()
  (interactive)
  (setq display-line-numbers 'relative))

(global-hl-line-mode 1)

(use-package rainbow-mode :diminish rainbow-mode)

(if (fboundp 'mac-auto-operator-composition-mode)
    (mac-auto-operator-composition-mode t))

(blink-cursor-mode 0)
(column-number-mode)

(setq frame-title-format "emacs")

(when (boundp 'fringe-mode)
  (fringe-mode 20))

(setq blink-matching-paren 'jump-offscreen)
(show-paren-mode nil)

(setq-default cursor-type 'bar)
(setq-default truncate-lines nil)

(setq frame-title-format
      `((buffer-file-name "%f" "%b")))

(when (and (image-type-available-p 'image-io)
           (not (boundp 'imagemagick-render-type)))
  (setq imagemagick-enabled-types t)
  (setq imagemagick-types-inhibit
        (cons 'XML (delq 'PDF imagemagick-types-inhibit)))
  (imagemagick-register-types))
#+END_SRC

** Diminish
#+BEGIN_SRC emacs-lisp
(use-package diminish
  :config
  (eval-after-load "undo-tree"
    '(diminish 'undo-tree-mode))
  (eval-after-load "subword"
    '(diminish 'subword-mode))
  (diminish 'auto-fill-function)
  (diminish 'org-indent-mode)
  (diminish 'visual-line-mode)
  (diminish 'abbrev-mode)
  (diminish 'eldoc-mode))
#+END_SRC

** Editing helpers
#+BEGIN_SRC emacs-lisp
(electric-indent-mode t)
(electric-pair-mode t)
(use-package expand-region :commands er/expand-region)
#+END_SRC

** Emacs Lisp
#+BEGIN_SRC emacs-lisp
(add-to-list 'auto-mode-alist '("\\.el\\'" . emacs-lisp-mode))
(define-key emacs-lisp-mode-map (kbd "C-c C-c") 'eval-buffer)

(add-hook 'emacs-lisp-mode-hook
  (lambda ()
    (setq mode-name
          '("Elisp"
            (:eval (if lexical-binding
                       "/l"
                     (propertize "/d" 'face 'nil)))))))
#+END_SRC

** Keybindings
#+BEGIN_SRC emacs-lisp
(use-package windresize)

(defun find-file-right (filename)
  (interactive)
  (split-window-right)
  (other-window 1)
  (find-file filename))

(defun find-file-below (filename)
  (interactive)
  (split-window-below)
  (other-window 1)
  (find-file filename))

(use-package which-key
  :diminish which-key-mode
  :config
  (which-key-mode))

(use-package general
  :config

  (general-create-definer my-leader-def
    :prefix "SPC")

  (my-leader-def
    :states 'normal
    :keymaps 'override
    "a" 'org-agenda
    "b" 'consult-buffer
    "1" 'treemacs
    "c" (lambda () (interactive) (org-capture))
    "d" 'deadgrep
    "e" 'consult-recent-file
    "h" 'highlight-symbol-at-point
    "H" 'unhighlight-regexp
    "i" 'abo/consult-heading
    "je" (lambda () (interactive) (find-file "~/.emacs.d/init.org"))
    "jg" (lambda () (interactive) (find-file "~/Dropbox/notes/gtd.org"))
    "ji" (lambda () (interactive) (find-file "~/Dropbox/notes/inbox.org"))
    "jj" (lambda () (interactive) (find-file "~/Dropbox/notes/journal.org"))
    "jr" 'abo/weekly-review
    "jn" (lambda () (interactive) (find-file "~/Dropbox/notes/"))
    "jp" (lambda () (interactive) (find-file "~/projects/"))
    "jw" (lambda () (interactive) (find-file "~/work"))
    "k" 'org-capture
    "p" 'project-switch-project
    "o" 'consult-outline
    "F" 'rg
    "f" 'consult-ripgrep
    "gg" 'magit-status
    "gf" 'magit-file-dispatch
    "gs" 'abo/open-in-sublime-merge
    "m" 'consult-mark
    "M" 'consult-global-mark
    "r" 'consult-ripgrep
    "s" 'speedbar-get-focus
    "t" 'consult-fd
    ;; "t" (lambda () (interactive) (org-capture nil "t"))
    "w" 'er/expand-region
    "W" 'er/contract-region
    "x" 'emamux:run-last-command
    "X" 'emamux:send-command
    "SPC" 'abo-run
    )

  (defun abo/consult-heading ()
    (interactive)
    (if (derived-mode-p 'org-mode)
        (consult-org-heading)
      (consult-outline)))

  (defun abo-run ()
    (interactive)
    (cond
     ((derived-mode-p 'org-mode) (call-interactively #'org-ctrl-c-ctrl-c))
     ((derived-mode-p 'emacs-lisp-mode) (call-interactively #'eval-buffer))
     (t (message "nothing to run for this buffer"))))

  (my-leader-def
    :states 'visual
    :keymaps 'override
    "x" 'emamux:send-region)

  (winner-mode 1)

  (general-define-key
   :states 'normal
   "-" 'dired-jump
   "[[" 'previous-buffer
   "]]" 'next-buffer
    ":" 'evil-ex
    "-" 'dired-jump
   )

  (general-define-key
   :states 'insert
   "s-/" 'hippie-expand
   "M-/" 'hippie-expand)

  (general-define-key
   :keymaps 'override

   "s-t" 'consult-fd
   "M-t" 'consult-fd

   "<f5>" 'ispell-buffer
   "<f6>" 'iedit-mode
   "<f7>" 'org-tree-slide-mode
   "S-<f7>" 'org-tree-slide-skip-done-toggle
   ;; Hydra on F8
   "<f9>" 'deft

   "M-." 'xref-find-definitions
   "M-c" 'kill-ring-save ; ⌘-c = Copy
   "M-v" 'yank ; ⌘-v = Paste
   ;; "M-x" 'counsel-M-x

   "C-h" 'tmux-move-left
   "C-j" 'tmux-move-down
   "C-l" 'tmux-move-right
   "C-k" 'tmux-move-up

   "C-r" 'undo-redo

   "C-c C-m" 'execute-extended-command ; Another =M-x= without leaving the home row
   "C-c C-q" 'org-set-tags-command

   "C-c 9" 'paredit-backward-slurp-sexp
   "C-c 0" 'paredit-forward-slurp-sexp
   "C-c [" 'paredit-backward-barf-sexp
   "C-c ]" 'paredit-forward-barf-sexp
   "C-c a" 'org-agenda
   "C-c d" 'deadgrep
   "C-c e" 'er/expand-region
   ;; C-c C-c "runs" what makes sense for a particular mode
   "C-c gl" 'git-link
   "C-c gs" 'abo/open-in-sublime-merge
   "C-c gt" 'git-timemachine-toggle
   "C-c jc" 'org-clock-jump-to-current-clock
   "C-c k" 'org-capture
   "C-c K" 'org-agenda
   "C-c l" 'org-store-link

   "C-c of" 'auto-fill-mode
   "C-c og" 'global-hl-line-mode
   "C-c oi" 'electric-indent-mode
   "C-c olh" 'hide-line-numbers
   "C-c oll" 'show-line-numbers
   "C-c olr" 'show-relative-line-numbers
   "C-c op" 'show-paren-mode
   "C-c or" 'rainbow-mode
   "C-c ot" 'toggle-truncate-lines
   "C-c ow" 'visual-line-mode
   "C-c s" 'consult-ripgrep
   "C-c t" 'tab-switcher

   "C-c p" 'project-find-file
   "C-c r" 'consult-recent-file
   "C-c R" 'revert-buffer
   "C-c w r" 'windresize

   "C-x C-m" 'execute-extended-command
   "C-x b" 'consult-buffer
   "C-x C-f" 'find-file
   "C-x B" 'project-switch-to-buffer
   "C-x m" 'execute-extended-command ; Another =M-x= without leaving the home row
   "C-x o" 'other-window)
  )
#+END_SRC

** Notes and writing
#+BEGIN_SRC emacs-lisp
(use-package deft
  :commands (deft)
  :init
  (setq deft-extensions '("org" "md")
        deft-recursive t
        deft-directory "~/Dropbox/notes"))

(use-package markdown-mode
  :mode "\\.md\\'")

(setq
 time-stamp-active t
 time-stamp-line-limit 30
 time-stamp-format "%04y-%02m-%02d")

(use-package writeroom-mode
  :commands writeroom-mode
  :bind (("C-c w w" . writeroom-mode)))

(add-hook 'before-save-hook #'time-stamp)

(use-package writegood-mode
  :commands writegood-mode)

(require 'browse-url)

(defun my-lookup-wikipedia ()
  "Look up the word under cursor in Wikipedia."
  (interactive)
  (let (word)
    (setq word
          (if (use-region-p)
              (buffer-substring-no-properties (region-beginning) (region-end))
            (current-word)))
    (setq word (replace-regexp-in-string " " "_" word))
    (xwidget-webkit-browse-url (concat "http://en.wikipedia.org/wiki/" word))))

(use-package biblio
  :commands biblio-lookup)
#+END_SRC

** Dired
#+BEGIN_SRC emacs-lisp
(setq ls-lisp-use-insert-directory-program t)
(setq dired-listing-switches "-alh")
(when (string-equal system-type "darwin")
  (setq insert-directory-program "/opt/homebrew/bin/gls"))

(require 'dired)
(defun my-dired-mode-setup ()
  "to be run as hook for `dired-mode'."
  (dired-hide-details-mode 1))
(add-hook 'dired-mode-hook 'my-dired-mode-setup)

(put 'dired-find-alternate-file 'disabled nil)
(setq dired-dwim-target t)
(add-hook 'dired-load-hook
          (lambda ()
            (load "dired-x")
            (setq dired-recursive-copies (quote always))
            (setq dired-recursive-deletes (quote top))))

(eval-after-load "dired"
  '(progn
     (define-key dired-mode-map "-" 'dired-up-directory)
     (define-key dired-mode-map (kbd "g") nil)
     (define-key dired-mode-map "gg" 'beginning-of-buffer)
     (define-key dired-mode-map "G" 'end-of-buffer)))

(use-package dired-rsync
  :bind (:map dired-mode-map ("b" . dired-rsync)))

(setq dired-guess-shell-alist-user '(("\\.pdf\\'" "open")))

(defun dired-open-file-with-system-viewer ()
  "Open the file at point in Dired using the system's default application."
  (interactive)
  (let ((file (dired-get-file-for-visit)))
    (start-process "dired-open-file" nil "open" file)))

(defun dired-find-file-or-open ()
  "Open PDF files with the system viewer, other files with `dired-find-file`."
  (interactive)
  (let ((file (dired-get-file-for-visit)))
    (if (string-match "\\.pdf\\'" file)
        (dired-open-file-with-system-viewer)
      (dired-find-file))))

(with-eval-after-load 'dired
  (define-key dired-mode-map (kbd "RET") 'dired-find-file-or-open))
#+END_SRC

** Evil
#+BEGIN_SRC emacs-lisp
(use-package evil
  :init (setq evil-want-C-i-jump nil)
  :config
  (define-key evil-normal-state-map (kbd "C-r") 'undo-redo)
  (define-key evil-normal-state-map (kbd "C-n") 'next-line)
  (define-key evil-normal-state-map (kbd "C-p") 'previous-line)  (define-key evil-normal-state-map (kbd "M-,") 'xref-pop-marker-stack)
  (define-key evil-normal-state-map (kbd "M-.") 'xref-find-definitions)
  (evil-mode 1)

  (defun my-evil-record-macro ()
    (interactive)
    (if buffer-read-only
        (call-interactively 'quit-window)
      (call-interactively 'evil-record-macro)))

  (with-eval-after-load 'evil-maps
    (define-key evil-normal-state-map (kbd "q") 'my-evil-record-macro))

  (evil-set-initial-state 'deadgrep-mode 'emacs)
  (evil-set-initial-state 'rg-mode 'emacs)
  (evil-set-initial-state 'deft-mode 'insert)
  (evil-set-initial-state 'dired-mode 'normal)
  (evil-set-initial-state 'use-package-statistics 'emacs)
  (evil-set-initial-state 'xref--xref-buffer-mode 'emacs)
  (evil-set-initial-state 'term-mode 'emacs)
  (evil-set-initial-state 'ert-results-mode 'emacs)
  (evil-set-initial-state 'vterm-mode 'emacs)
  (evil-set-initial-state 'shell-mode 'emacs)
  
  

  (add-hook 'with-editor-mode-hook 'evil-insert-state)

  (evil-ex-define-cmd "W" 'save-buffer)
  (evil-define-key 'insert lisp-interaction-mode-map (kbd "C-c C-c") 'eval-print-last-sexp))

;; Extra evil plugins disabled in ultra-fast notes profile.

(setq evil-insert-state-cursor '((bar . 2) "black")
      evil-normal-state-cursor '(box "black")
      evil-visual-state-cursor '(box "black")
      evil-emacs-state-cursor '((box . 2) "#d787af"))
#+END_SRC

** Tramp
#+BEGIN_SRC emacs-lisp
(require 'tramp)
(add-to-list 'tramp-remote-path "~/.local/share/npm/bin/")
(add-to-list 'tramp-remote-path 'tramp-own-remote-path)
(setq tramp-default-method "ssh"
      tramp-verbose 1)

(setq vc-ignore-dir-regexp
      (format "\\(%s\\)\\|\\(%s\\)"
              vc-ignore-dir-regexp
              tramp-file-name-regexp))
#+END_SRC

** Tmux integration
#+BEGIN_SRC emacs-lisp
(use-package emamux
  :commands (emamux:run-last-command emamux:send-command emamux:send-region)
  :init
  (setq emamux:use-nearest-pane 1))
#+END_SRC

** Misc packages
#+BEGIN_SRC emacs-lisp
(require 'uniquify)
(setq uniquify-buffer-name-style 'forward)
#+END_SRC

** AI assistants (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** Compilation (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

* Org
** General settings
#+BEGIN_SRC emacs-lisp
(setq org-directory "~/Dropbox/notes")
(setq org-id-locations-file
      (expand-file-name ".org-id-locations" org-directory))
(setq org-archive-location "archives/%s_archive::")

(defun abo/refresh-org-agenda-files ()
  "Recompute `org-agenda-files' from `org-directory'."
  (interactive)
  (setq org-agenda-files (directory-files-recursively org-directory "\\.org$")))

(defun abo/ensure-org-agenda-files (&rest _)
  "Populate `org-agenda-files' on first agenda usage."
  (unless org-agenda-files
    (abo/refresh-org-agenda-files)))

(setq org-agenda-files nil)
(add-hook 'emacs-startup-hook
          (lambda ()
            (run-with-idle-timer 5 nil #'abo/refresh-org-agenda-files)))
(advice-add 'org-agenda :before #'abo/ensure-org-agenda-files)
(advice-add 'org-refile :before #'abo/ensure-org-agenda-files)

(defun abo/org-ensure-archive-directory ()
  (when (and buffer-file-name
             (derived-mode-p 'org-mode)
             (file-in-directory-p (file-truename buffer-file-name)
                                  (file-truename org-directory)))
    (let ((archive-dir (expand-file-name "archives" (file-name-directory buffer-file-name))))
      (unless (file-directory-p archive-dir)
        (make-directory archive-dir t)))))

(add-hook 'org-mode-hook 'abo/org-ensure-archive-directory)
(add-hook 'org-mode-hook 'turn-on-auto-fill)
(add-hook 'org-mode-hook 'abbrev-mode)

(require 'org-tempo)

(setq org-todo-keywords
      '((sequence "TODO(t)" "NEXT(n)" "WAITING(w@/!)" "|" "DONE(d!)" "CANCELED(c!)")))

(setq org-fontify-quote-and-verse-blocks t)
(setq org-hide-emphasis-markers nil)

(add-hook 'org-mode-hook
          (lambda ()
            (setq-local time-stamp-start "Updated on[ 	]+\\\\?[\"<]+")
            (org-indent-mode t)
            (add-hook 'before-save-hook 'time-stamp nil 'local)))

(add-hook 'org-mode-hook
      '(lambda ()
             (setq org-file-apps
                   (append '(("\\.jpg\\'" . default)
                             ("\\.jpeg\\'" . default)
                             ("\\.png\\'" . default)
                             ("\\.pdf\\'" . default))
                           org-file-apps))))

(with-eval-after-load 'org-mode
  (define-key org-mode-map (kbd "C-c C-s") 'org-schedule))
#+END_SRC

** Habits
#+BEGIN_SRC emacs-lisp
(require 'org-habit)
(add-to-list 'org-modules 'org-habit)
(setq org-log-into-drawer t)
(setq org-habit-show-habits t)
(setq org-habit-graph-column 50)
#+END_SRC

** Agenda
#+BEGIN_SRC emacs-lisp
(setq org-refile-targets '((nil :maxlevel . 5)
                           (org-agenda-files :maxlevel . 5)))
(advice-add 'org-refile :after
            (lambda (&rest _)
              (org-save-all-org-buffers)))
#+END_SRC

#+begin_src emacs-lisp
(setq org-stuck-projects
      '("+project/-MAYBE-DONE" ("NEXT") ("errand")
        "\\<IGNORE\\>"))
(add-to-list 'org-tags-exclude-from-inheritance "project")
#+end_src

** Links
#+BEGIN_SRC emacs-lisp
(defun my-org-replace-link-file (from to)
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward org-bracket-link-regexp nil t)
      (when (string-match-p from (match-string 1))
        (replace-match (concat "[[file:" to "]]"))))))

(defun my-org-rename-link-file-at-point ()
  "Rename or move a file in an external link at point and update the link path"
  (interactive)
  (let* ((curr-dir (abbreviate-file-name default-directory))
         (current-path (org-element-property :path (org-element-context)))
         (new-path (read-file-name "Rename file at point to: " current-path)))
    (rename-file current-path new-path)
    (message (concat "moved to: " new-path))
    (if (directory-name-p new-path)
        (setq new-path (concat new-path (file-name-nondirectory current-path)))
      (setq new-path new-path))
    (my-org-replace-link-file current-path
                              (replace-regexp-in-string curr-dir "" new-path))))

(setq org-id-link-to-org-use-id 'create-if-interactive)
#+END_SRC

** Capture
#+BEGIN_SRC emacs-lisp
(setq org-capture-templates
      '(("i" "inbox" entry (file+headline "~/Dropbox/notes/inbox.org" "Inbox") "* %?\n")
        ("h" "things-inbox" entry (file+headline "~/Dropbox/notes/inbox.org" "Inbox")
         "* TODO [THINGS] %?\nCaptured: %U\n")
        ("j" "Journal" entry (file+datetree "~/Dropbox/notes/journal.org") "* %?" :empty-lines 1)
        ("t" "todo-inbox" entry (file+headline "~/Dropbox/notes/inbox.org" "Inbox")
         "* TODO %?\nSCHEDULED: %(org-insert-time-stamp (org-read-date nil t \"+0d\"))\n%a\n")))

(defun abo/delete-capture-frame (&rest _)
  "Close the frame after capture when using global capture."
  (when (equal "global-org-capture" (frame-parameter nil 'name))
    (delete-frame)))

(advice-add 'org-capture-finalize :after #'abo/delete-capture-frame)
(advice-add 'org-capture-destroy :after #'abo/delete-capture-frame)

(add-hook 'org-capture-mode-hook 'delete-other-windows)

(defun abo/weekly-review ()
  "Open core GTD buffers for weekly review."
  (interactive)
  (delete-other-windows)
  (find-file "~/Dropbox/notes/gtd.org")
  (split-window-right)
  (other-window 1)
  (find-file "~/Dropbox/notes/inbox.org")
  (split-window-below)
  (other-window 1)
  (find-file "~/Dropbox/notes/journal.org")
  (other-window -2))
#+END_SRC

** Babel (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** Export and publishing (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** LaTeX (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** Presentations (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** Attachments and references (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** Archive
#+BEGIN_SRC emacs-lisp
(add-hook 'org-archive-hook 'org-save-all-org-buffers)
#+END_SRC

* Utilities
** Date
In org mode you can do ~C-c !~ to pick and insert the date

#+BEGIN_SRC emacs-lisp
(defun abo/today ()
  "Today's date as a string."
  (format-time-string "%Y-%m-%d"))

(defun abo/add-date-to-filename ()
  "Add current date in front of filename for current buffer."
  (interactive)
  (let* ((date (abo/today))
         (buffer-file (buffer-file-name))
         (new-file-name (concat (file-name-directory buffer-file)
                                date "-"
                                (file-name-nondirectory buffer-file))))
    (save-buffer)
    (rename-file buffer-file new-file-name)
    (set-visited-file-name new-file-name)
    (save-buffer)))

(defun abo/insert-date ()
  "Insert today's date in current buffer"
  (interactive)
  (insert (abo/today)))
#+END_SRC

** File
#+BEGIN_SRC emacs-lisp
(defun abo/change-line-endings-to-unix ()
  (let ((coding-str (symbol-name buffer-file-coding-system)))
    (when (string-match "-\\(?:dos\\|mac\\)$" coding-str)
      (set-buffer-file-coding-system 'unix))))

(defun filepath-with-line-number-for-current-buffer ()
  "Return a string with Buffer-file-name:line-number."
  (interactive)
  (concat (buffer-file-name) ":" (number-to-string (line-number-at-pos))))

(defun abo/open-in-sublime-merge (file)
  "Open FILE in Sublime Merge.
Without a prefix argument, use the current buffer file.
With `C-u', prompt for any file."
  (interactive
   (list
    (if (and buffer-file-name (not current-prefix-arg))
        buffer-file-name
      (read-file-name "Open in Sublime Merge: "
                      default-directory
                      buffer-file-name
                      t))))
  (unless (eq system-type 'darwin)
    (user-error "This command currently supports macOS only"))
  (unless (file-exists-p file)
    (user-error "File does not exist: %s" file))
  (start-process "open-in-sublime-merge" nil "open" "-a" "Sublime Merge" (expand-file-name file)))
#+END_SRC

** Evil modeline colors
#+BEGIN_SRC emacs-lisp
(defun abo/bg-modeline-color-from-evil-state ()
  (interactive)
  (cond ((evil-insert-state-p) "#87af87")
        ((evil-visual-state-p) "#87afaf")
        ((evil-emacs-state-p) "#d787af")
        ((evil-normal-state-p) "#ffaf00")
        (t "#3c3836")))

(defun abo/fg-modeline-color-from-evil-state ()
  (interactive)
  (cond ((evil-insert-state-p) "#3c3836")
        ((evil-visual-state-p) "#3c3836")
        ((evil-emacs-state-p) "#3c3836")
        ((evil-normal-state-p) "#3c3836")
        (t "#ebddb2")))

(defun abo/post-command-evil-modeline-colors-hook ()
  (interactive)
  (set-face-background 'mode-line (abo/bg-modeline-color-from-evil-state))
  (set-face-foreground 'mode-line (abo/fg-modeline-color-from-evil-state)))

;; (add-hook 'post-command-hook 'abo/post-command-evil-modeline-colors-hook)
#+END_SRC

* Navigation
** Project
#+BEGIN_SRC emacs-lisp
(require 'project)
(setq project-switch-commands 'project-dired)
(setq project-find-functions '(project-try-vc))

(setq project-find-file-function
      (lambda ()
        (let ((default-directory (project-root (project-current t))))
          (call-interactively 'consult-ripgrep))))
#+END_SRC

** Completion framework
#+BEGIN_SRC emacs-lisp
(use-package vertico
  :init (vertico-mode))

(use-package orderless
  :init
  (setq completion-styles '(orderless basic)
        completion-category-defaults nil
        completion-category-overrides '((file (styles partial-completion)))))

(use-package consult
  :bind ("C-c f" . consult-fd))

(use-package embark
  :bind (("C-o" . embark-act)
         ("C-;" . embark-dwim)
         ("C-h B" . embark-bindings))
  :init
  (setq prefix-help-command #'embark-prefix-help-command)
  :config
  (define-key embark-file-map (kbd "t") #'find-file-other-tab)
  (define-key embark-buffer-map (kbd "t") #'switch-to-buffer-other-tab))

(use-package embark-consult
  :after (embark consult)
  :hook (embark-collect-mode . consult-preview-at-point-mode))
#+END_SRC

** File browser (disabled)
#+BEGIN_SRC emacs-lisp
;; Disabled in notes-only ultra-fast profile.
#+END_SRC

** Search
#+BEGIN_SRC emacs-lisp
(use-package wgrep :defer t)
(require 'abo/find-in-project)
(use-package iedit
  :commands iedit-mode)
(use-package deadgrep
  :commands deadgrep)
(use-package rg
  :commands rg)
#+END_SRC

