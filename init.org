#+TITLE: Emacs configuration
#+PROPERTY: header-args:emacs-lisp :tangle init-tangled.el :results silent
#+STARTUP: overview

#+begin_src emacs-lisp :tangle no
(native-comp-available-p)
#+end_src

My favorite way to install emacs on OSX
#+begin_src sh :tangle no
brew tap d12frosted/emacs-plus
brew install --cask emacs-plus-app
#+end_src

#+begin_src emacs-lisp :tangle no
(native-compile-async "~/.emacs.d/elpa/" 'recursively)
#+end_src

* Configuration
** Initialization
#+BEGIN_SRC emacs-lisp
;;; -*- lexical-binding: t; -*-

(eval-when-compile
  (require 'autorevert)
  (require 'dired)
  (require 'dired-x)
  (require 'elec-pair)
  (require 'eshell)
  (require 'hydra nil t)
  (require 'evil nil t)
  (require 'ls-lisp)
  (require 'org)
  (require 'org-element)
  (require 'project)
  (require 'recentf)
  (require 'savehist)
  (require 'saveplace)
  (require 'speedbar nil t)
  (require 'time-stamp))

;; Declared for byte-compiler in minimal batch contexts.
(defvar package--init-file-ensured)
(defvar reb-re-syntax)
(defvar org-capture-templates)
(defvar org-babel-clojure-backend)
(defvar org-babel-js-function-wrapper)
(defvar org-ditaa-jar-path)
(defvar org-download-timestamp-format)
(defvar project-find-file-function)
(declare-function default-text-scale-increment "default-text-scale" (&optional increment))
(declare-function default-text-scale-reset "default-text-scale" ())
(declare-function dired-get-file-for-visit "dired")
(declare-function dired-find-file "dired")
(declare-function dired-hide-details-mode "dired-x" (&optional arg))
(declare-function evil-window-right "evil-window" (&optional count))
(declare-function evil-window-left "evil-window" (&optional count))
(declare-function evil-window-up "evil-window" (&optional count))
(declare-function evil-window-down "evil-window" (&optional count))
(declare-function evil-set-initial-state "evil-core" (mode state))
(declare-function evil-ex-define-cmd "evil-commands" (cmd func))
(declare-function evil-define-key* "evil-core" (state keymap key def &rest bindings))
(declare-function evil-insert-state-p "evil-states" ())
(declare-function evil-visual-state-p "evil-states" ())
(declare-function evil-emacs-state-p "evil-states" ())
(declare-function evil-normal-state-p "evil-states" ())
(declare-function hydra-default-pre "hydra")
(declare-function hydra-keyboard-quit "hydra")
(declare-function hydra--call-interactively-remap-maybe "hydra")
(declare-function hydra-show-hint "hydra")
(declare-function hydra-set-transient-map "hydra")
(declare-function org-ctrl-c-ctrl-c "org")
(declare-function org-indent-mode "org-indent" (&optional arg))
(declare-function org-save-all-org-buffers "org")
(declare-function org-element-context "org-element")
(declare-function org-link-display-format "org")
(declare-function org-back-to-heading "org")
(declare-function org-get-heading "org")
(declare-function org-outline-level "org")
(declare-function org-map-entries "org")
(declare-function org-end-of-meta-data "org")
(declare-function org-element--property "org-element")

(defun abo/tangle-init-config ()
  "Tangle `init.org' into `init-tangled.el'."
  (interactive)
  (let ((init-org (expand-file-name "init.org" user-emacs-directory))
        (init-el (expand-file-name "init-tangled.el" user-emacs-directory)))
    (org-babel-tangle-file init-org init-el)
    (when (file-exists-p init-el)
      (let ((byte-compile-warnings '(not obsolete cl-functions))
            (warning-minimum-level :error)
            (inhibit-message t)
            (message-log-max nil))
        (ignore-errors
          (byte-compile-file init-el)))
      (when (fboundp 'native-compile-async)
        (ignore-errors
          (native-compile-async init-el nil))))))

(defun abo/tangle-init-config-on-save ()
  "Auto-tangle the literate config when saving `init.org'."
  (when (and buffer-file-name
             (string-equal (file-truename buffer-file-name)
                           (file-truename (expand-file-name "init.org" user-emacs-directory))))
    (abo/tangle-init-config)))

(defun abo/setup-init-org-auto-tangle ()
  "Enable local auto-tangle when visiting `init.org'."
  (when (and buffer-file-name
             (string-equal (file-truename buffer-file-name)
                           (file-truename (expand-file-name "init.org" user-emacs-directory))))
    (add-hook 'after-save-hook #'abo/tangle-init-config-on-save nil t)))

(add-hook 'org-mode-hook #'abo/setup-init-org-auto-tangle)


(defun my-reload-dir-locals-for-current-buffer ()
  "Reloads dir locals for the current buffer."
  (interactive)
  (let ((enable-local-variables :all))
    (hack-dir-local-variables-non-file-buffer)))

(defun my-reload-dir-locals-for-all-buffer-in-this-directory ()
  "Reload dir-locals in buffers sharing the current `default-directory`."
  (interactive)
  (let ((dir default-directory))
    (dolist (buffer (buffer-list))
      (with-current-buffer buffer
        (when (equal default-directory dir)
          (my-reload-dir-locals-for-current-buffer))))))

(setq initial-major-mode 'fundamental-mode
      initial-scratch-message nil
      inhibit-startup-message t)

(pixel-scroll-precision-mode)

(setq vc-follow-symlinks t)
(put 'narrow-to-region 'disabled nil)
#+END_SRC

** abo-initialization
#+BEGIN_SRC emacs-lisp
(defvar abo/default-file-name-handler-alist file-name-handler-alist)
(defvar abo/default-gc-cons-threshold (* 16 1024 1024))
(defvar abo/default-gc-cons-percentage 0.1)

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold abo/default-gc-cons-threshold
                  gc-cons-percentage abo/default-gc-cons-percentage
                  file-name-handler-alist abo/default-file-name-handler-alist)
            (garbage-collect)))

;; Run GC while idle to reduce pauses during interactive editing.
(unless noninteractive
  (run-with-idle-timer
   5 t
   (lambda ()
     (unless (minibufferp)
       (garbage-collect)))))

(setq read-process-output-max (* 1024 1024)
      process-adaptive-read-buffering nil)

(when (boundp 'inhibit-compacting-font-caches)
  (setq inhibit-compacting-font-caches t))
(when (boundp 'fast-but-imprecise-scrolling)
  (setq fast-but-imprecise-scrolling t))
(when (boundp 'redisplay-skip-fontification-on-input)
  (setq redisplay-skip-fontification-on-input t))
(when (boundp 'native-comp-async-report-warnings-errors)
  (setq native-comp-async-report-warnings-errors 'silent))

(setq byte-compile-warnings '(not obsolete cl-functions))

(setq package-archives
      '(("melpa"       . "https://melpa.org/packages/")
        ("gnu"         . "https://elpa.gnu.org/packages/")
        ("nongnu"      . "https://elpa.nongnu.org/nongnu/")))

(setq package-user-dir (concat user-emacs-directory "elpa"))

(setq package--init-file-ensured t) ; do not add things at the end of init.el
(setq package-check-signature 'allow-unsigned)
(setq package-quickstart t)

(package-initialize)

(defun abo/refresh-package-contents ()
  "Refresh package archives on demand."
  (interactive)
  (package-refresh-contents))

(defun require-package (package &optional min-version)
  "Install PACKAGE via ELPA when missing. Optional MIN-VERSION is supported."
  (unless (package-installed-p package min-version)
    (unless package-archive-contents
      (package-refresh-contents))
    (package-install package)))

(require-package 'use-package)
(require 'use-package)

(require-package 'diminish)
(require 'diminish)

(setq use-package-compute-statistics nil) ; set to t temporarily when profiling startup
(setq use-package-always-ensure nil)

(setq custom-file (concat user-emacs-directory "custom.el"))
(condition-case err
    (load custom-file 'noerror 'nomessage)
  (error
   (message "Skipping invalid custom.el entry: %s" (error-message-string err))))

(if (file-exists-p "~/.emacs.d/.emacs-local")
    (load "~/.emacs.d/.emacs-local"))

(let ((site-lisp-dir (expand-file-name "site-lisp" user-emacs-directory)))
  (when (file-directory-p site-lisp-dir)
    (add-to-list 'load-path site-lisp-dir)
    (dolist (dir (directory-files site-lisp-dir t "^[^.]" t))
      (when (and (file-directory-p dir)
                 (directory-files dir nil "\\.el\\'" t))
        (add-to-list 'load-path dir)))))
#+END_SRC

** Environment and paths
#+BEGIN_SRC emacs-lisp
(let ((my-java-home "/Library/Java/JavaVirtualMachines/adoptopenjdk-12.0.2.jdk/Contents/Home"))
  (when (file-directory-p my-java-home)
    (setenv "JAVA_HOME" my-java-home)))
(setenv "OBJC_DISABLE_INITIALIZE_FORK_SAFETY" "YES")

(let* ((home-folder (getenv "HOME"))
       (my-paths `("/opt/homebrew/bin"
                   ,(concat home-folder "/work/jeancaisse/node_modules/.bin")
                   "/Applications/Postgres.app/Contents/Versions/latest/bin"
                   "/opt/homebrew/opt/grep/libexec/gnubin"
                   "/opt/homebrew/opt/gnu-sed/libexec/gnubin"
                   "/opt/homebrew/opt/findutils/libexec/gnubin"
                   "/opt/homebrew/opt/coreutils/libexec/gnubin"
                   ,(concat home-folder "/.asdf/shims/")
                   ,(concat home-folder "/.config/yarn/global/node_modules/.bin/")
                   ,(concat home-folder "/.local/share/n/bin")
                   ,(concat home-folder "/work/dox-compose/bin/")
                   ,(concat home-folder "/dotfiles/bin/")
                   ,(concat home-folder "/.fzf/bin")
                   ,(concat home-folder "/.local/bin")
                   ,(concat home-folder "/.local/share/npm/bin/")
                   ,(concat home-folder "/bin")
                   "/snap/bin"
                   "/usr/local/bin"
                   "/bin/"
                   "/usr/bin/"
                   "/usr/local/sbin/"
                   "/opt/homebrew/opt/openjdk/bin/"
                   ,(concat home-folder "/.cargo/bin/")))
       (existing-paths nil)
       (seen-paths (make-hash-table :test 'equal))
       (path-separator-string (if (characterp path-separator)
                                  (char-to-string path-separator)
                                path-separator))
       (path-split-regexp (regexp-quote path-separator-string)))
  ;; Preserve inherited PATH/exec-path while prioritizing custom entries.
  (dolist (path (append my-paths
                        exec-path
                        (split-string (or (getenv "PATH") "") path-split-regexp t)))
    (when (and (stringp path)
               (not (gethash path seen-paths)))
      (puthash path t seen-paths)
      (push path existing-paths)))
  (setq existing-paths (nreverse existing-paths))
  (setenv "PATH" (mapconcat #'identity existing-paths path-separator-string))
  (setq exec-path existing-paths))

(use-package exec-path-from-shell
  :if (memq window-system '(mac ns x))
  :commands exec-path-from-shell-copy-env)

(when (memq window-system '(mac ns x))
  (exec-path-from-shell-copy-env "SSH_AGENT_PID")
  (exec-path-from-shell-copy-env "SSH_AUTH_SOCK"))

(setenv "FZF_DEFAULT_OPTS" "--color=light")
#+END_SRC

** Performance
#+BEGIN_SRC emacs-lisp
(setq large-file-warning-threshold (* 2 1024 1024))

(defun abo/check-large-file-hook ()
  "Use a lightweight setup for very large local text files."
  (let* ((file (buffer-file-name))
         (ext (downcase (or (file-name-extension (or file "")) ""))))
    (when (and file
               (not (file-remote-p file))
               (> (buffer-size) large-file-warning-threshold)
               (not (member ext '("jpg" "jpeg" "png" "gif" "webp" "svg" "pdf"))))
      (fundamental-mode)
      (font-lock-mode -1)
      (setq-local buffer-read-only t)
      (buffer-disable-undo))))
(add-hook 'find-file-hook #'abo/check-large-file-hook)

(setq-default bidi-paragraph-direction 'left-to-right)
(setq bidi-inhibit-bpa t)
(setq auto-window-vscroll nil)

(global-so-long-mode 1)
#+END_SRC

** Backups and autosave
#+BEGIN_SRC emacs-lisp
(let ((save-dir (expand-file-name "~/.local/share/emacs-saves/")))
  (unless (file-directory-p save-dir)
    (make-directory save-dir t))
  (setq backup-by-copying t
      backup-directory-alist `((".*" . ,save-dir))
      delete-old-versions t
      kept-new-versions 6
      kept-old-versions 2
      delete-by-moving-to-trash t
      auto-save-default t
      auto-save-timeout 60
      auto-save-interval 200
      version-control t
      create-lockfiles nil
      auto-save-file-name-transforms `((".*" ,save-dir t))))

(setq global-auto-revert-non-file-buffers t)
(setq auto-revert-verbose nil)
(setq auto-revert-remote-files nil)
#+END_SRC

** Encoding
#+BEGIN_SRC emacs-lisp
(setq default-file-name-coding-system 'utf-8-unix)
(setq file-name-coding-system 'utf-8-unix)
(setq default-process-coding-system '(utf-8-unix . utf-8-unix))
(prefer-coding-system 'utf-8)
(modify-coding-system-alist 'process "\\*compilation\\*\\'" 'utf-8)
#+END_SRC

** General settings
#+BEGIN_SRC emacs-lisp
(setq help-window-select t
      indicate-empty-lines nil)

(make-variable-buffer-local 'compile-command)
(defalias 'yes-or-no-p 'y-or-n-p)
(setq ring-bell-function 'ignore)
(setq visible-bell t)

(pending-delete-mode 1)

(setq sentence-end-double-space nil)

(add-hook 'shell-mode-hook 'compilation-shell-minor-mode)

(setq-default indent-tabs-mode nil
              c-basic-offset 2)

(setq recentf-max-menu-items 200)
(setq recentf-max-saved-items 200)
(setq recentf-auto-cleanup 'never)
(defvar recentf-exclude nil)
(with-eval-after-load 'recentf
  (dolist (pattern '("/tmp/" "/ssh:" "/sudo:" "/scp:"))
    (add-to-list 'recentf-exclude pattern)))

(let ((state-dir (expand-file-name ".cache/state/" user-emacs-directory)))
  (unless (file-directory-p state-dir)
    (make-directory state-dir t))
  (setq savehist-file (expand-file-name "history" state-dir)
        save-place-file (expand-file-name "places" state-dir)))
(setq history-length 200)
(setq savehist-additional-variables
      '(mark-ring global-mark-ring search-ring regexp-search-ring))

(defvar abo/stateful-modes-enabled nil)

(defun abo/enable-stateful-modes ()
  "Enable stateful modes after startup to keep init fast."
  (unless abo/stateful-modes-enabled
    (recentf-mode 1)
    (savehist-mode 1)
    (save-place-mode 1)
    (global-auto-revert-mode 1)
    (setq abo/stateful-modes-enabled t)))

(unless noninteractive
  (run-with-idle-timer 1 nil #'abo/enable-stateful-modes))

(setq warning-minimum-level :error)
#+END_SRC

** Whitespace
#+BEGIN_SRC emacs-lisp
(setq-default whitespace-style '(face trailing))

(defun abo/delete-trailing-whitespace-maybe ()
  "Delete trailing whitespace in regular text/code buffers."
  (when (and (derived-mode-p 'prog-mode 'text-mode 'conf-mode)
             (not buffer-read-only)
             (< (buffer-size) (* 1024 1024)))
    (delete-trailing-whitespace)))

(add-hook 'before-save-hook #'abo/delete-trailing-whitespace-maybe)
(add-hook 'prog-mode-hook 'whitespace-mode)
(eval-after-load "whitespace"
  '(diminish 'whitespace-mode))
#+END_SRC

** UI and display
#+BEGIN_SRC emacs-lisp
(global-display-line-numbers-mode -1)
(defun show-line-numbers ()
  (interactive)
  (setq display-line-numbers 'absolute))
(defun hide-line-numbers ()
  (interactive)
  (setq display-line-numbers 'nil))
(defun show-relative-line-numbers ()
  (interactive)
  (setq display-line-numbers 'relative))

(global-hl-line-mode 1)

(use-package rainbow-mode
  :commands (rainbow-mode)
  :diminish rainbow-mode)

(if (fboundp 'mac-auto-operator-composition-mode)
    (mac-auto-operator-composition-mode t))

(blink-cursor-mode 0)
(column-number-mode)

(setq frame-title-format "emacs")

(when (boundp 'fringe-mode)
  (fringe-mode 20))

(setq blink-matching-paren 'jump-offscreen)
(show-paren-mode nil)

(setq-default cursor-type 'bar)
(setq-default truncate-lines nil)

(setq frame-title-format
      `((buffer-file-name "%f" "%b")))

(when (and (image-type-available-p 'image-io)
           (not (boundp 'imagemagick-render-type)))
  (setq imagemagick-enabled-types t)
  (setq imagemagick-types-inhibit
        (cons 'XML (delq 'PDF imagemagick-types-inhibit)))
  (imagemagick-register-types))
#+END_SRC

** Diminish
#+BEGIN_SRC emacs-lisp
(use-package diminish
  :config
  (eval-after-load "undo-tree"
    '(diminish 'undo-tree-mode))
  (eval-after-load "subword"
    '(diminish 'subword-mode))
  (diminish 'auto-fill-function)
  (diminish 'org-indent-mode)
  (diminish 'visual-line-mode)
  (diminish 'abbrev-mode)
  (diminish 'eldoc-mode))
#+END_SRC

** Editing helpers
#+BEGIN_SRC emacs-lisp
(electric-indent-mode t)

(electric-pair-mode t)
(setq electric-pair-inhibit-predicate
      (lambda (c)
        (or (minibufferp)
            (eq major-mode 'org-mode)
            (not (or
                  (char-equal c ?\s)
                  (char-equal c ?\t)
                  (eolp))))))

(use-package paredit
  :diminish paredit-mode
  :hook ((emacs-lisp-mode . paredit-mode)
         (clojure-mode . paredit-mode)))

(use-package expand-region
  :commands (er/expand-region er/contract-region))

(defun abo/origami-safe-background ()
  "Return a valid background color for Origami header faces."
  (let ((default-bg (face-attribute 'default :background nil t)))
    (if (and (stringp default-bg)
             (not (string-prefix-p "unspecified" default-bg)))
        default-bg
      "#d9d9d9")))

(defun abo/origami-valid-color-p (value)
  "Return non-nil when VALUE can safely be used as a face color."
  (and (stringp value)
       (not (string-prefix-p "unspecified" value))))

(defun abo/origami-fix-header-face ()
  "Patch Origami face definitions that can break on newer Emacs versions."
  (let ((bg (abo/origami-safe-background)))
    (when (facep 'origami-fold-header-face)
      (condition-case nil
          (set-face-attribute 'origami-fold-header-face nil
                              :box '(:line-width 1)
                              :background bg)
        (error nil)))))

(defvar abo/origami-load-error-reported nil)

(defun abo/origami-enable-safely ()
  "Enable Origami with compatible face settings."
  (unless (abo/origami-valid-color-p
           (face-attribute 'highlight :background nil t))
    (set-face-attribute 'highlight nil
                        :background (abo/origami-safe-background)))
  (condition-case err
      (progn
        (require 'origami)
        (abo/origami-fix-header-face)
        (origami-mode 1))
    (error
     (unless abo/origami-load-error-reported
       (setq abo/origami-load-error-reported t)
       (message "Origami disabled: %s" (error-message-string err))))))

(use-package origami
  :if (not noninteractive)
  :commands (origami-mode)
  :hook (prog-mode . abo/origami-enable-safely))

(with-eval-after-load 're-builder
  (setq reb-re-syntax 'string))

(defun my-prog-mode-auto-fill-hook ()
  (setq fill-column 100)
  (set (make-local-variable 'comment-auto-fill-only-comments) t)
  (auto-fill-mode t))
(add-hook 'prog-mode-hook 'my-prog-mode-auto-fill-hook)

(use-package highlight-blocks
  :defer t)

(defun sudo ()
  "Use TRAMP to `sudo' the file for current buffer."
  (interactive)
  (when buffer-file-name
    (find-alternate-file
     (concat "/sudo:root@localhost:"
             buffer-file-name))))

(defun enable-minor-mode (my-pair)
  "Enable minor mode if filename match the regexp. MY-PAIR is a
cons cell (regexp . minor-mode)."
  (if (buffer-file-name)
      (if (string-match (car my-pair) buffer-file-name)
          (funcall (cdr my-pair)))))
#+END_SRC

** Emacs Lisp
#+BEGIN_SRC emacs-lisp
(add-to-list 'auto-mode-alist '("\\.el\\'" . emacs-lisp-mode))
(define-key emacs-lisp-mode-map (kbd "C-c C-c") 'eval-buffer)

(add-hook 'emacs-lisp-mode-hook
  (lambda ()
    (setq mode-name
          '("Elisp"
            (:eval (if lexical-binding
                       "/l"
                     (propertize "/d" 'face 'nil)))))))
#+END_SRC

** Keybindings
#+BEGIN_SRC emacs-lisp
(use-package windresize
  :commands (windresize))

(defun find-file-right (filename)
  (interactive)
  (split-window-right)
  (other-window 1)
  (find-file filename))

(defun find-file-below (filename)
  (interactive)
  (split-window-below)
  (other-window 1)
  (find-file filename))

(use-package which-key
  :defer 1
  :diminish which-key-mode
  :config
  (which-key-mode))

(use-package general
  :config

  (defun abo/move-left ()
    "Move to left tmux pane when available, else use windmove."
    (interactive)
    (if (commandp 'tmux-move-left)
        (call-interactively 'tmux-move-left)
      (call-interactively 'windmove-left)))

  (defun abo/move-down ()
    "Move to lower tmux pane when available, else use windmove."
    (interactive)
    (if (commandp 'tmux-move-down)
        (call-interactively 'tmux-move-down)
      (call-interactively 'windmove-down)))

  (defun abo/move-right ()
    "Move to right tmux pane when available, else use windmove."
    (interactive)
    (if (commandp 'tmux-move-right)
        (call-interactively 'tmux-move-right)
      (call-interactively 'windmove-right)))

  (defun abo/move-up ()
    "Move to upper tmux pane when available, else use windmove."
    (interactive)
    (if (commandp 'tmux-move-up)
        (call-interactively 'tmux-move-up)
      (call-interactively 'windmove-up)))

  (general-define-key
   :states 'normal
   :keymaps 'override
   "SPC a" 'org-agenda
   "SPC b" 'consult-buffer
   "SPC 1" 'treemacs
   "SPC c" (lambda () (interactive) (org-capture))
   "SPC d" 'deadgrep
   "SPC e" 'consult-recent-file
   "SPC h" 'highlight-symbol-at-point
   "SPC H" 'unhighlight-regexp
   "SPC i" 'abo/consult-heading
   "SPC je" (lambda () (interactive) (find-file "~/.emacs.d/init.org"))
   "SPC jg" (lambda () (interactive) (find-file "~/Dropbox/notes/gtd.org"))
   "SPC ji" (lambda () (interactive) (find-file "~/Dropbox/notes/inbox.org"))
   "SPC jj" (lambda () (interactive) (find-file "~/Dropbox/notes/journal.org"))
   "SPC jr" 'abo/weekly-review
   "SPC jn" (lambda () (interactive) (find-file "~/Dropbox/notes/"))
   "SPC jp" (lambda () (interactive) (find-file "~/projects/"))
   "SPC jw" (lambda () (interactive) (find-file "~/work"))
   "SPC k" 'org-capture
   "SPC p" 'project-switch-project
   "SPC o" 'consult-outline
   "SPC F" 'rg
   "SPC f" 'consult-ripgrep
   "SPC gg" 'magit-status
   "SPC gf" 'magit-file-dispatch
   "SPC gs" 'abo/open-in-sublime-merge
   "SPC m" 'consult-mark
   "SPC M" 'consult-global-mark
   "SPC r" 'consult-ripgrep
   "SPC s" 'speedbar-get-focus
   "SPC t" 'consult-fd
   ;; "SPC t" (lambda () (interactive) (org-capture nil "t"))
   "SPC w" 'er/expand-region
   "SPC W" 'er/contract-region
   "SPC x" 'emamux:run-last-command
   "SPC X" 'emamux:send-command
   "SPC SPC" 'abo-run)

  (defun abo/consult-heading ()
    (interactive)
    (if (derived-mode-p 'org-mode)
        (consult-org-heading)
      (consult-outline)))

  (defun abo-run ()
    (interactive)
    (cond
     ((derived-mode-p 'org-mode) (call-interactively #'org-ctrl-c-ctrl-c))
     ((derived-mode-p 'emacs-lisp-mode) (call-interactively #'eval-buffer))
     (t (message "nothing to run for this buffer"))))

  (general-define-key
   :states 'visual
   :keymaps 'override
   "SPC x" 'emamux:send-region)

  (winner-mode 1)

  (general-define-key
   :states 'normal
   "-" 'dired-jump
   "[[" 'previous-buffer
   "]]" 'next-buffer
    ":" 'evil-ex
    "-" 'dired-jump
   )

  (general-define-key
   :states 'insert
   "s-/" 'hippie-expand
   "M-/" 'hippie-expand)

  (general-define-key
   :keymaps 'override

   "s-t" 'consult-fd
   "M-t" 'consult-fd

   "<f5>" 'ispell-buffer
   "<f6>" 'iedit-mode
   "<f7>" 'org-tree-slide-mode
   "S-<f7>" 'org-tree-slide-skip-done-toggle
   ;; Hydra on F8
   "<f9>" 'deft

   "M-." 'xref-find-definitions
   "M-c" 'kill-ring-save ; ⌘-c = Copy
   "M-v" 'yank ; ⌘-v = Paste
   ;; "M-x" 'counsel-M-x

   "C-h" 'abo/move-left
   "C-j" 'abo/move-down
   "C-l" 'abo/move-right
   "C-k" 'abo/move-up

   "C-r" 'undo-redo

   "C-c C-m" 'execute-extended-command ; Another =M-x= without leaving the home row
   "C-c C-q" 'org-set-tags-command

   "C-c 9" 'paredit-backward-slurp-sexp
   "C-c 0" 'paredit-forward-slurp-sexp
   "C-c [" 'paredit-backward-barf-sexp
   "C-c ]" 'paredit-forward-barf-sexp
   "C-c a" 'org-agenda
   "C-c d" 'deadgrep
   "C-c e" 'er/expand-region
   ;; C-c C-c "runs" what makes sense for a particular mode
   "C-c gl" 'git-link
   "C-c gs" 'abo/open-in-sublime-merge
   "C-c gt" 'git-timemachine-toggle
   "C-c jc" 'org-clock-jump-to-current-clock
   "C-c k" 'org-capture
   "C-c K" 'org-agenda
   "C-c l" 'org-store-link

   "C-c of" 'auto-fill-mode
   "C-c og" 'global-hl-line-mode
   "C-c oi" 'electric-indent-mode
   "C-c olh" 'hide-line-numbers
   "C-c oll" 'show-line-numbers
   "C-c olr" 'show-relative-line-numbers
   "C-c op" 'show-paren-mode
   "C-c or" 'rainbow-mode
   "C-c ot" 'toggle-truncate-lines
   "C-c ow" 'visual-line-mode
   "C-c s" 'consult-ripgrep
   "C-c t" 'tab-switcher

   "C-c p" 'project-find-file
   "C-c r" 'consult-recent-file
   "C-c R" 'revert-buffer
   "C-c w r" 'windresize

   "C-x C-m" 'execute-extended-command
   "C-x b" 'consult-buffer
   "C-x C-f" 'find-file
   "C-x B" 'project-switch-to-buffer
   "C-x m" 'execute-extended-command ; Another =M-x= without leaving the home row
   "C-x o" 'other-window)
  )

(use-package hydra
  :config
  (defhydra hydra-utils (global-map "<f8>")
    "drag"
    ("j" drag-stuff-down "down")
    ("k" drag-stuff-up "up")))

(use-package drag-stuff
  :defer 2
  :diminish drag-stuff-mode
  :config
  (drag-stuff-global-mode t))

(defun my/sublime-mark-next ()
  "Behave exactly like Sublime Text's Cmd+D.
   1. If no region is active, select the symbol at point.
   2. If a region IS active, mark the next occurrence."
  (interactive)
  (if (region-active-p)
      (mc/mark-next-like-this 1)
    ;; If no selection, manually select the symbol under cursor
    (if-let ((bounds (bounds-of-thing-at-point 'symbol)))
        (progn
          (goto-char (car bounds))
          (push-mark (cdr bounds) t t))
      (user-error "No symbol at point"))))

(use-package multiple-cursors
  :bind (("C->" . my/sublime-mark-next)
         ("C-<" . mc/mark-previous-like-this)
         ("C-c C-<" . mc/mark-all-like-this)))
#+END_SRC

** Notes and writing
#+BEGIN_SRC emacs-lisp
(use-package deft
  :commands (deft)
  :init
  (setq deft-extensions '("org" "md")
        deft-recursive t
        deft-directory "~/Dropbox/notes"))

(use-package markdown-mode
  :mode "\\.md\\'")

(setq
 time-stamp-active t
 time-stamp-line-limit 30
 time-stamp-format "%04y-%02m-%02d")

(use-package writeroom-mode
  :commands writeroom-mode
  :bind (("C-c w w" . writeroom-mode)))

(add-hook 'before-save-hook #'time-stamp)

(use-package writegood-mode
  :commands writegood-mode)

(defun my-lookup-wikipedia ()
  "Look up the word under cursor in Wikipedia."
  (interactive)
  (let (word)
    (setq word
          (if (use-region-p)
              (buffer-substring-no-properties (region-beginning) (region-end))
            (current-word)))
    (setq word (replace-regexp-in-string " " "_" word))
    (xwidget-webkit-browse-url (concat "http://en.wikipedia.org/wiki/" word))))

(use-package biblio
  :commands biblio-lookup)
#+END_SRC

** Dired
#+BEGIN_SRC emacs-lisp
(setq ls-lisp-use-insert-directory-program t)
(setq dired-listing-switches "-alh")
(when (string-equal system-type "darwin")
  (setq insert-directory-program "/opt/homebrew/bin/gls"))

(defun my-dired-mode-setup ()
  "to be run as hook for `dired-mode'."
  (dired-hide-details-mode 1))
(add-hook 'dired-mode-hook 'my-dired-mode-setup)

(put 'dired-find-alternate-file 'disabled nil)
(setq dired-dwim-target t)
(with-eval-after-load 'dired
  (require 'dired-x)
  (setq dired-recursive-copies 'always)
  (setq dired-recursive-deletes 'top))

(eval-after-load "dired"
  '(progn
     (define-key dired-mode-map "-" 'dired-up-directory)
     (define-key dired-mode-map (kbd "g") nil)
     (define-key dired-mode-map "gg" 'beginning-of-buffer)
     (define-key dired-mode-map "G" 'end-of-buffer)))

(use-package dired-rsync
  :bind (:map dired-mode-map ("b" . dired-rsync)))

(setq dired-guess-shell-alist-user '(("\\.pdf\\'" "open")))

(defun dired-open-file-with-system-viewer ()
  "Open the file at point in Dired using the system's default application."
  (interactive)
  (let ((file (dired-get-file-for-visit)))
    (start-process "dired-open-file" nil "open" file)))

(defun dired-find-file-or-open ()
  "Open PDF files with the system viewer, other files with `dired-find-file`."
  (interactive)
  (let ((file (dired-get-file-for-visit)))
    (if (string-match "\\.pdf\\'" file)
        (dired-open-file-with-system-viewer)
      (dired-find-file))))

(with-eval-after-load 'dired
  (define-key dired-mode-map (kbd "RET") 'dired-find-file-or-open))
#+END_SRC

** Evil
#+BEGIN_SRC emacs-lisp
(use-package evil
  :init (setq evil-want-C-i-jump nil)
  :config
  (define-key evil-normal-state-map (kbd "C-r") 'undo-redo)
  (define-key evil-normal-state-map (kbd "C-n") 'next-line)
  (define-key evil-normal-state-map (kbd "C-p") 'previous-line)  (define-key evil-normal-state-map (kbd "M-,") 'xref-pop-marker-stack)
  (define-key evil-normal-state-map (kbd "M-.") 'xref-find-definitions)
  (evil-mode 1)

  (defun my-evil-record-macro ()
    (interactive)
    (if buffer-read-only
        (call-interactively 'quit-window)
      (call-interactively 'evil-record-macro)))

  (with-eval-after-load 'evil-maps
    (define-key evil-normal-state-map (kbd "q") 'my-evil-record-macro))

  (evil-set-initial-state 'deadgrep-mode 'emacs)
  (evil-set-initial-state 'rg-mode 'emacs)
  (evil-set-initial-state 'deft-mode 'insert)
  (evil-set-initial-state 'dired-mode 'normal)
  (evil-set-initial-state 'use-package-statistics 'emacs)
  (evil-set-initial-state 'xref--xref-buffer-mode 'emacs)
  (evil-set-initial-state 'term-mode 'emacs)
  (evil-set-initial-state 'ert-results-mode 'emacs)
  (evil-set-initial-state 'vterm-mode 'emacs)
  (evil-set-initial-state 'shell-mode 'emacs)
  (evil-set-initial-state 'tab-switcher-mode 'emacs)  (evil-set-initial-state 'ivy-occur-mode 'emacs)
  (evil-set-initial-state 'ivy-occur-grep-mode 'emacs)  (evil-set-initial-state 'compilation-mode 'emacs)

  (add-hook 'with-editor-mode-hook 'evil-insert-state)

  (evil-ex-define-cmd "W" 'save-buffer)
  (evil-define-key 'insert lisp-interaction-mode-map (kbd "C-c C-c") 'eval-print-last-sexp))

(use-package evil-surround
  :after evil
  :config
  (global-evil-surround-mode 1))

(use-package evil-commentary
  :after evil
  :diminish evil-commentary-mode
  :config
  (evil-commentary-mode))

(use-package evil-visualstar
  :after evil
  :config
  (evil-define-key nil evil-normal-state-map (kbd "k") 'evil-previous-visual-line)
  (evil-define-key nil evil-normal-state-map (kbd "j") 'evil-next-visual-line)
  (global-evil-visualstar-mode t))

(use-package evil-matchit
  :defer 2
  :after evil
  :config
  (global-evil-matchit-mode 1))

(use-package key-chord
  :after evil
  :config
  (key-chord-mode 1)
  (key-chord-define evil-insert-state-map "jk" 'evil-normal-state))

(setq evil-insert-state-cursor '((bar . 2) "black")
      evil-normal-state-cursor '(box "black")
      evil-visual-state-cursor '(box "black")
      evil-emacs-state-cursor '((box . 2) "#d787af"))
#+END_SRC

** Tramp
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'tramp
  (add-to-list 'tramp-remote-path "~/.local/share/npm/bin/")
  (add-to-list 'tramp-remote-path 'tramp-own-remote-path)
  (setq tramp-default-method "ssh"
        tramp-verbose 1)
  (setq vc-ignore-dir-regexp
        (format "\\(%s\\)\\|\\(%s\\)"
                vc-ignore-dir-regexp
                tramp-file-name-regexp)))
#+END_SRC

** Tmux integration
#+BEGIN_SRC emacs-lisp
(defun tmux-socket-command-string ()
  "Return tmux socket command prefix from $TMUX."
  (interactive)
  (concat "tmux -S "
          (replace-regexp-in-string "\n\\'" ""
                                    (shell-command-to-string "echo $TMUX | sed -e 's/,.*//g'"))))

(defun tmux-move-right ()
  (interactive)
  (condition-case nil
      (evil-window-right 1)
    (error
     (unless window-system
       (shell-command (concat (tmux-socket-command-string) " select-pane -R") nil)))))

(defun tmux-move-left ()
  (interactive)
  (condition-case nil
      (evil-window-left 1)
    (error
     (unless window-system
       (shell-command (concat (tmux-socket-command-string) " select-pane -L") nil)))))

(defun tmux-move-up ()
  (interactive)
  (condition-case nil
      (evil-window-up 1)
    (error
     (unless window-system
       (shell-command (concat (tmux-socket-command-string) " select-pane -U") nil)))))

(defun tmux-move-down ()
  (interactive)
  (condition-case nil
      (evil-window-down 1)
    (error
     (unless window-system
       (shell-command (concat (tmux-socket-command-string) " select-pane -D") nil)))))

(use-package emamux
  :commands (emamux:run-last-command emamux:send-command emamux:send-region)
  :init
  (setq emamux:use-nearest-pane 1))
#+END_SRC

** Misc packages
#+BEGIN_SRC emacs-lisp
(use-package restclient
  :mode "\\.http\\'")

(use-package hyperbole
  :if (not noninteractive)
  :defer 2
  :diminish hyperbole-mode
  :init
  (let ((hyperbole-dir (expand-file-name "hyperbole/" user-emacs-directory)))
    (unless (file-directory-p hyperbole-dir)
      (make-directory hyperbole-dir t))
    (setq hbmap:dir-user hyperbole-dir))
  :config
  (hyperbole-mode 1))

(use-package sqlite3 :defer t)
(use-package jsonrpc :defer t)

(require 'uniquify)
(setq uniquify-buffer-name-style 'forward)

(use-package hideshow :defer t)
(with-eval-after-load 'hideshow
  (require 'hideshowvis))

(use-package envrc
  :defer 2
  :diminish envrc-mode
  :config
  (envrc-global-mode))
#+END_SRC

** AI assistants
#+BEGIN_SRC emacs-lisp
(defun abo/add-straight-build-to-load-path (package-name)
  "Keep legacy straight packages available without bootstrapping straight."
  (let ((package-dir (expand-file-name
                      (format "straight/build/%s" package-name)
                      user-emacs-directory)))
    (when (file-directory-p package-dir)
      (add-to-list 'load-path package-dir))))

(abo/add-straight-build-to-load-path "copilot")

(defun abo/install-ai-packages ()
  (interactive)
  (unless (fboundp 'package-vc-install)
    (user-error "package-vc-install is unavailable on this Emacs"))
  (unless (locate-library "copilot")
    (package-vc-install "https://github.com/copilot-emacs/copilot.el")))

(use-package copilot
  :if (and (not noninteractive)
           (locate-library "copilot"))
  :commands (copilot-mode copilot-accept-completion copilot-accept-completion-by-word)
  :hook (prog-mode . copilot-mode)
  :bind (:map copilot-completion-map
              ("<tab>" . 'copilot-accept-completion)
              ("TAB" . 'copilot-accept-completion)
              ("C-TAB" . 'copilot-accept-completion-by-word)
              ("C-<tab>" . 'copilot-accept-completion-by-word)))
#+END_SRC

** Compilation
#+BEGIN_SRC emacs-lisp
(use-package ansi-color
  :hook (compilation-filter . ansi-color-compilation-filter))

(with-eval-after-load 'compile
  (add-to-list 'compilation-error-regexp-alist-alist
               '(node "^\\([a-zA-Z\\.0-9/-]+\\):\\([0-9]+\\)$" 1 2))
  (add-to-list 'compilation-error-regexp-alist 'node)
  (add-to-list 'compilation-error-regexp-alist-alist
               '(rspec "^rspec \\(.*\\):\\([0-9]+\\)" 1 2))
  (add-to-list 'compilation-error-regexp-alist 'rspec))
#+END_SRC

* Org
** General settings
#+BEGIN_SRC emacs-lisp
(setq org-directory "~/Dropbox/notes")
(setq org-id-locations-file
      (expand-file-name ".org-id-locations" org-directory))
(setq org-archive-location "archives/%s_archive::")

(defun abo/refresh-org-agenda-files ()
  "Recompute `org-agenda-files' from `org-directory'."
  (interactive)
  (setq org-agenda-files (directory-files-recursively org-directory "\\.org$")))

(defun abo/ensure-org-agenda-files (&rest _)
  "Populate `org-agenda-files' on first agenda usage."
  (unless org-agenda-files
    (abo/refresh-org-agenda-files)))

(unless (and (boundp 'org-agenda-files) org-agenda-files)
  (setq org-agenda-files nil))
(unless (advice-member-p #'abo/ensure-org-agenda-files 'org-agenda)
  (advice-add 'org-agenda :before #'abo/ensure-org-agenda-files))
(unless (advice-member-p #'abo/ensure-org-agenda-files 'org-refile)
  (advice-add 'org-refile :before #'abo/ensure-org-agenda-files))

(defun abo/org-ensure-archive-directory ()
  (when (and buffer-file-name
             (derived-mode-p 'org-mode)
             (file-in-directory-p (file-truename buffer-file-name)
                                  (file-truename org-directory)))
    (let ((archive-dir (expand-file-name "archives" (file-name-directory buffer-file-name))))
      (unless (file-directory-p archive-dir)
        (make-directory archive-dir t)))))

(add-hook 'org-mode-hook 'abo/org-ensure-archive-directory)
(add-hook 'org-mode-hook 'turn-on-auto-fill)
(add-hook 'org-mode-hook 'abbrev-mode)

(with-eval-after-load 'org
  (require 'org-tempo))

(setq org-todo-keywords
      '((sequence "TODO(t)" "NEXT(n)" "WAITING(w@/!)" "|" "DONE(d!)" "CANCELED(c!)")))

(setq org-fontify-quote-and-verse-blocks t)
(setq org-hide-emphasis-markers nil)

(add-hook 'org-mode-hook
          (lambda ()
            (setq-local time-stamp-start "Updated on[ 	]+\\\\?[\"<]+")
            (org-indent-mode t)
            (add-hook 'before-save-hook 'time-stamp nil 'local)))

(add-hook 'org-mode-hook
      #'(lambda ()
             (setq org-file-apps
                   (append '(("\\.jpg\\'" . default)
                             ("\\.jpeg\\'" . default)
                             ("\\.png\\'" . default)
                             ("\\.pdf\\'" . default))
                           org-file-apps))))

(with-eval-after-load 'org-mode
  (define-key org-mode-map (kbd "C-c C-s") 'org-schedule))
#+END_SRC

** Habits
#+BEGIN_SRC emacs-lisp
(setq org-log-into-drawer t)
(with-eval-after-load 'org-agenda
  (require 'org-habit)
  (add-to-list 'org-modules 'org-habit)
  (setq org-habit-show-habits t)
  (setq org-habit-graph-column 50))
#+END_SRC

** Agenda
#+BEGIN_SRC emacs-lisp
(setq org-refile-targets '((nil :maxlevel . 5)
                           (org-agenda-files :maxlevel . 5)))
(defun abo/save-all-org-buffers-after-refile (&rest _)
  (org-save-all-org-buffers))

(unless (advice-member-p #'abo/save-all-org-buffers-after-refile 'org-refile)
  (advice-add 'org-refile :after #'abo/save-all-org-buffers-after-refile))
#+END_SRC

#+begin_src emacs-lisp
(setq org-stuck-projects
      '("+project/-MAYBE-DONE" ("NEXT") ("errand")
        "\\<IGNORE\\>"))
(with-eval-after-load 'org
  (add-to-list 'org-tags-exclude-from-inheritance "project"))
#+end_src

** Links
#+BEGIN_SRC emacs-lisp
(defun my-org-replace-link-file (from to)
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward org-link-bracket-re nil t)
      (when (string-match-p from (match-string 1))
        (replace-match (concat "[[file:" to "]]"))))))

(defun my-org-rename-link-file-at-point ()
  "Rename or move a file in an external link at point and update the link path"
  (interactive)
  (let* ((curr-dir (abbreviate-file-name default-directory))
         (current-path (org-element-property :path (org-element-context)))
         (new-path (read-file-name "Rename file at point to: " current-path)))
    (rename-file current-path new-path)
    (message (concat "moved to: " new-path))
    (if (directory-name-p new-path)
        (setq new-path (concat new-path (file-name-nondirectory current-path)))
      (setq new-path new-path))
    (my-org-replace-link-file current-path
                              (replace-regexp-in-string curr-dir "" new-path))))

(setq org-id-link-to-org-use-id 'create-if-interactive)
#+END_SRC

** Capture
#+BEGIN_SRC emacs-lisp
(setq org-capture-templates
      '(("i" "inbox" entry (file+headline "~/Dropbox/notes/inbox.org" "Inbox") "* %?\n")
        ("h" "things-inbox" entry (file+headline "~/Dropbox/notes/inbox.org" "Inbox")
         "* TODO [THINGS] %?\nCaptured: %U\n")
        ("j" "Journal" entry (file+olp+datetree "~/Dropbox/notes/journal.org") "* %?" :empty-lines 1)
        ("t" "todo-inbox" entry (file+headline "~/Dropbox/notes/inbox.org" "Inbox")
         "* TODO %?\nSCHEDULED: %(org-insert-time-stamp (org-read-date nil t \"+0d\"))\n%a\n")))

(defun abo/delete-capture-frame (&rest _)
  "Close the frame after capture when using global capture."
  (when (equal "global-org-capture" (frame-parameter nil 'name))
    (delete-frame)))

(advice-add 'org-capture-finalize :after #'abo/delete-capture-frame)
(advice-add 'org-capture-destroy :after #'abo/delete-capture-frame)

(add-hook 'org-capture-mode-hook 'delete-other-windows)

(defun abo/weekly-review ()
  "Open core GTD buffers for weekly review."
  (interactive)
  (delete-other-windows)
  (find-file "~/Dropbox/notes/gtd.org")
  (split-window-right)
  (other-window 1)
  (find-file "~/Dropbox/notes/inbox.org")
  (split-window-below)
  (other-window 1)
  (find-file "~/Dropbox/notes/journal.org")
  (other-window -2))
#+END_SRC

** Babel
#+BEGIN_SRC emacs-lisp
  (use-package ob-clojurescript :defer t)
  (use-package ob-mermaid :defer t)
  (setq org-babel-clojure-backend 'cider)
  (setq org-babel-js-function-wrapper "require('util').log(require('util').inspect(function(){%s}()));")
  (setq ob-mermaid-cli-path "mmdc")

  (defvar abo/org-babel-languages-loaded nil)

  (defun abo/load-org-babel-languages (&rest _)
    "Load heavier Org Babel integrations on demand."
    (unless abo/org-babel-languages-loaded
      (require 'ob-clojure)
      (require 'ob-js)
      (require 'ob-ruby)
      (let ((languages '((shell . t)
                         (sql . t)
                         (ditaa . t))))
        (when (locate-library "ob-mermaid")
          (push '(mermaid . t) languages))
        (org-babel-do-load-languages 'org-babel-load-languages (nreverse languages)))
      (setq org-ditaa-jar-path "/usr/local/Cellar/ditaa/0.11.0/libexec/ditaa-0.11.0-standalone.jar")
      (setq abo/org-babel-languages-loaded t)))

  (defun abo/org--clean-heading (text)
    "Return heading text with links and targets removed."
    (let ((clean (org-link-display-format text)))
      (setq clean (replace-regexp-in-string "<<+\\([^>]+\\)>>+" "\\1" clean))
      (setq clean (replace-regexp-in-string "[\t ]+" " " clean))
      (string-trim clean)))

  (defun abo/org-subtree-to-mermaid-mindmap (outfile)
    "Insert a Mermaid mindmap block for the current Org subtree."
    (interactive
     (save-excursion
       (org-back-to-heading t)
       (let* ((root (org-get-heading t t t t))
              (root-text (abo/org--clean-heading root))
              (base-name (if buffer-file-name
                             (file-name-base buffer-file-name)
                           "mindmap"))
              (slug (downcase (replace-regexp-in-string "[^a-z0-9]+" "-" root-text)))
              (default-file (format "%s-%s.svg" base-name (if (string= slug "") "mindmap" slug))))
         (list (read-file-name "Write Mermaid image to: " nil nil nil default-file)))))
    (org-back-to-heading t)
    (let* ((root (org-get-heading t t t t))
           (root-text (abo/org--clean-heading root))
           (base-level (org-outline-level))
           (file-arg (format "%S" outfile))
           (lines (list (format "#+begin_src mermaid :file %s :results file :exports results" file-arg)
                        "mindmap"
                        (format "  root((%s))" root-text)))
           (child-lines nil))
      (org-map-entries
       (lambda ()
         (let* ((level (org-outline-level))
                (title (org-get-heading t t t t))
                (title-text (abo/org--clean-heading title)))
           (unless (= level base-level)
             (let ((indent (+ 2 (* 2 (- level base-level)))))
               (push (format "%s%s" (make-string indent ? ) title-text) child-lines)))))
       nil 'tree)
      (setq lines (append lines (nreverse child-lines) '("#+end_src")))
      (save-excursion
        (org-end-of-meta-data t)
        (insert "\n" (mapconcat #'identity lines "\n") "\n\n"))))


  (unless (advice-member-p #'abo/load-org-babel-languages 'org-babel-execute-src-block)
    (advice-add 'org-babel-execute-src-block :before #'abo/load-org-babel-languages))
#+END_SRC

** Export and publishing
#+BEGIN_SRC emacs-lisp
(use-package htmlize :defer t)

(defvar abo/org-export-configured nil)

(defun abo/setup-org-export (&rest _)
  "Load Org export stack and related settings only when needed."
  (unless abo/org-export-configured
    (require 'ox-publish)
    (require 'htmlize)
    (setq system-time-locale "C")
    (setq org-html-validation-link nil)
    (setq org-publish-project-alist
          `(("blog-files"
             :base-directory "~/perso/aurelienbottazini.github.io/_org"
             :base-extension "org"
             :publishing-directory "~/perso/aurelienbottazini.github.io/"
             :recursive t
             :publishing-function org-html-publish-to-html
             :headline-levels 4
             :auto-preamble t
             :html-head-extra nil
             :body-only t))
          user-full-name "Aurélien Bottazini"
          org-export-with-toc t
          org-html-doctype "html5"
          org-html-head "<link rel=\"stylesheet\" type=\"text/css\" href=\"/css/main.css\" />"
          org-html-head-include-default-style nil
          org-html-head-include-scripts nil
          org-html-html5-fancy t
          org-html-postamble nil
          org-src-preserve-indentation nil
          org-html-htmlize-output-type "css"
          org-html-indent nil
          org-edit-src-content-indentation 0)
    (setq abo/org-export-configured t)))

(dolist (fn '(org-export-dispatch
              org-html-export-to-html
              org-publish
              org-publish-current-file))
  (unless (advice-member-p #'abo/setup-org-export fn)
    (advice-add fn :before #'abo/setup-org-export)))

(defun abo/toggle-html-export-on-save ()
  "Enable or disable HTML export when saving current org buffer."
  (interactive)
  (when (not (eq major-mode 'org-mode))
    (error "Not an org-mode file!"))
  (if (memq 'org-html-export-to-html after-save-hook)
      (progn (remove-hook 'after-save-hook 'org-html-export-to-html t)
             (message "Disabled org html export on save"))
    (abo/setup-org-export)
    (add-hook 'after-save-hook 'org-publish-current-file nil t)
    (set-buffer-modified-p t)
    (message "Enabled org html export on save")))
#+END_SRC

** LaTeX
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'ox-latex
  (add-to-list 'org-latex-packages-alist '("" "listings" nil))
  (setq org-latex-src-block-backend 'listings)
  (setq org-latex-listings-options '(("breaklines" "true")
                                     ("literate" "{0}{0}{1}%
           {1}{1}{1}%
           {2}{2}{1}%
           {3}{3}{1}%
           {4}{4}{1}%
           {5}{5}{1}%
           {6}{6}{1}%
           {7}{7}{1}%
           {8}{8}{1}%
           {9}{9}{1}%
    "))))
#+END_SRC

** Presentations
#+BEGIN_SRC emacs-lisp
(defun abott/org-tree-slide-play ()
  (writeroom-mode 1)
  (default-text-scale-increment 40))

(defun abott/org-tree-slide-stop ()
  (writeroom-mode -1)
  (default-text-scale-reset))

(use-package org-tree-slide
  :commands (org-tree-slide-mode org-tree-slide-move-previous-tree org-tree-slide-move-next-tree)
  :hook ((org-tree-slide-play . abott/org-tree-slide-play)
         (org-tree-slide-stop . abott/org-tree-slide-stop))
  :config
  (with-eval-after-load "org-tree-slide"
    (define-key org-tree-slide-mode-map (kbd "<f8>") 'org-tree-slide-move-previous-tree)
    (define-key org-tree-slide-mode-map (kbd "<f9>") 'org-tree-slide-move-next-tree)))

(use-package ox-reveal
  :commands org-reveal-export-to-html
  :config
  (setq org-reveal-root "file:///Users/abo/.emacs.d/site-lisp/reveal.js-4.1.0"))
#+END_SRC

** Attachments and references
#+BEGIN_SRC emacs-lisp
(use-package org-download
  :commands (org-download-enable org-download-yank)
  :hook (dired-mode . org-download-enable))

(setq org-cite-global-bibliography '("~/Documents/notes/bibliography.bib"))
(use-package org-ref
  :commands (org-ref-insert-link org-ref-open-bibtex-notes)
  :config
  (require 'org-ref-isbn))

(setq org-download-timestamp-format "")
#+END_SRC

** Archive
#+BEGIN_SRC emacs-lisp
(add-hook 'org-archive-hook 'org-save-all-org-buffers)
#+END_SRC

* Utilities
** Date
In org mode you can do ~C-c !~ to pick and insert the date

#+BEGIN_SRC emacs-lisp
(defun abo/today ()
  "Today's date as a string."
  (format-time-string "%Y-%m-%d"))

(defun abo/add-date-to-filename ()
  "Add current date in front of filename for current buffer."
  (interactive)
  (let* ((date (abo/today))
         (buffer-file (buffer-file-name))
         (new-file-name (concat (file-name-directory buffer-file)
                                date "-"
                                (file-name-nondirectory buffer-file))))
    (save-buffer)
    (rename-file buffer-file new-file-name)
    (set-visited-file-name new-file-name)
    (save-buffer)))

(defun abo/insert-date ()
  "Insert today's date in current buffer"
  (interactive)
  (insert (abo/today)))
#+END_SRC

** File
#+BEGIN_SRC emacs-lisp
(defun abo/change-line-endings-to-unix ()
  (let ((coding-str (symbol-name buffer-file-coding-system)))
    (when (string-match "-\\(?:dos\\|mac\\)$" coding-str)
      (set-buffer-file-coding-system 'unix))))

(defun filepath-with-line-number-for-current-buffer ()
  "Return a string with Buffer-file-name:line-number."
  (interactive)
  (concat (buffer-file-name) ":" (number-to-string (line-number-at-pos))))

(defun abo/open-in-sublime-merge (file)
  "Open FILE in Sublime Merge.
Without a prefix argument, use the current buffer file.
With `C-u', prompt for any file."
  (interactive
   (list
    (if (and buffer-file-name (not current-prefix-arg))
        buffer-file-name
      (read-file-name "Open in Sublime Merge: "
                      default-directory
                      buffer-file-name
                      t))))
  (unless (eq system-type 'darwin)
    (user-error "This command currently supports macOS only"))
  (unless (file-exists-p file)
    (user-error "File does not exist: %s" file))
  (start-process "open-in-sublime-merge" nil "open" "-a" "Sublime Merge" (expand-file-name file)))
#+END_SRC

** Evil modeline colors
#+BEGIN_SRC emacs-lisp
(defun abo/bg-modeline-color-from-evil-state ()
  (interactive)
  (cond ((evil-insert-state-p) "#87af87")
        ((evil-visual-state-p) "#87afaf")
        ((evil-emacs-state-p) "#d787af")
        ((evil-normal-state-p) "#ffaf00")
        (t "#3c3836")))

(defun abo/fg-modeline-color-from-evil-state ()
  (interactive)
  (cond ((evil-insert-state-p) "#3c3836")
        ((evil-visual-state-p) "#3c3836")
        ((evil-emacs-state-p) "#3c3836")
        ((evil-normal-state-p) "#3c3836")
        (t "#ebddb2")))

(defun abo/post-command-evil-modeline-colors-hook ()
  (interactive)
  (set-face-background 'mode-line (abo/bg-modeline-color-from-evil-state))
  (set-face-foreground 'mode-line (abo/fg-modeline-color-from-evil-state)))

;; (add-hook 'post-command-hook 'abo/post-command-evil-modeline-colors-hook)
#+END_SRC

* Navigation
** Project
#+BEGIN_SRC emacs-lisp
(require 'project)
(setq project-switch-commands 'project-dired)
(setq project-find-functions '(project-try-vc))

(setq project-find-file-function
      (lambda ()
        (let ((default-directory (project-root (project-current t))))
          (call-interactively 'consult-ripgrep))))

(setq find-sibling-rules
      '(
        ("\\(.*\\).ts\\'" "\\1.spec.ts")
        ("\\(.*\\).spec.ts\\'" "\\1.ts")
        ))
#+END_SRC

** Completion framework
#+BEGIN_SRC emacs-lisp
(use-package vertico
  :init (vertico-mode))

(use-package orderless
  :init
  (setq completion-styles '(orderless basic)
        completion-category-defaults nil
        completion-category-overrides '((file (styles partial-completion)))))

(use-package consult
  :bind ("C-c f" . consult-fd))

(use-package embark
  :bind (("C-o" . embark-act)
         ("C-;" . embark-dwim)
         ("C-h B" . embark-bindings))
  :init
  (setq prefix-help-command #'embark-prefix-help-command)
  :config
  (define-key embark-file-map (kbd "t") #'find-file-other-tab)
  (define-key embark-buffer-map (kbd "t") #'switch-to-buffer-other-tab))

(use-package embark-consult
  :after (embark consult))
#+END_SRC

** File browser
#+BEGIN_SRC emacs-lisp
(setq speedbar-directory-unshown-regexp "^$")
#+END_SRC

** Search
#+BEGIN_SRC emacs-lisp
(use-package wgrep :defer t)
(autoload 'abo/find-file-with-similar-name "abo/find-in-project" nil t)
(autoload 'abo/project-guess-file "abo/find-in-project" nil t)
(autoload 'abo/project-find-file "abo/find-in-project" nil t)
(use-package iedit
  :commands iedit-mode)
(use-package deadgrep
  :commands deadgrep)
(use-package rg
  :commands rg)
#+END_SRC
